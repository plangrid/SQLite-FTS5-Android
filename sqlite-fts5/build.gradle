plugins {
    id 'de.undercouch.download' version '3.4.3'
}

ext {
    SQLITE_VERSION = '3220000'
    SQLITE_YEAR = '2018'
    SQLITE_BUNDLE_NAME = "sqlite-src-${SQLITE_VERSION}.zip"

    PUBLISH_GROUP_ID = 'com.plangrid.android'
    PUBLISH_ARTIFACT_ID = 'sqlite-fts5-android'
    PUBLISH_VERSION = '3.22.0'
}

apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DSQLITE_ZIP:String=${buildDir}/${SQLITE_BUNDLE_NAME}"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
    buildToolsVersion '28.0.3'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'io.requery:sqlite-android:3.22.0'
}

task downloadSqliteSource(type: Download) {
    src "http://www.sqlite.org/${SQLITE_YEAR}/${SQLITE_BUNDLE_NAME}"
    overwrite false
    dest buildDir
}

afterEvaluate {
    externalNativeBuildDebug.dependsOn downloadSqliteSource
    externalNativeBuildRelease.dependsOn downloadSqliteSource
}

apply from: 'https://raw.githubusercontent.com/plangrid/release-android-library/master/android-release-aar.gradle'
