plugins {
    id 'de.undercouch.download' version '3.4.3'
}

ext {
    SQLITE_BUNDLE_NAME = "sqlite-src-${versions.sqliteSource}.zip"

    PUBLISH_GROUP_ID = 'com.plangrid.android'
    PUBLISH_ARTIFACT_ID = 'sqlite-fts5-android'
    PUBLISH_VERSION = versions.sqlite
}

apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion versions.androidTargetSdk
    defaultConfig {
        minSdkVersion versions.androidMinSdk
        targetSdkVersion versions.androidTargetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DSQLITE_ZIP:String=${buildDir}/${SQLITE_BUNDLE_NAME} -DSQLITE_MAX_VARIABLE_NUMBER:String=2147483000"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
    buildToolsVersion versions.androidBuildTools
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "io.requery:sqlite-android:${versions.sqlite}"
}

task downloadSqliteSource(type: Download) {
    src "http://www.sqlite.org/${versions.sqliteYear}/${SQLITE_BUNDLE_NAME}"
    overwrite false
    dest buildDir
}

afterEvaluate {
    externalNativeBuildDebug.dependsOn downloadSqliteSource
    externalNativeBuildRelease.dependsOn downloadSqliteSource
}

apply from: 'https://raw.githubusercontent.com/plangrid/release-android-library/master/android-release-aar.gradle'
